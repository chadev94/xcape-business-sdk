buildscript {
    ext {
        springBootVersion = '3.0.0'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.4.RELEASE"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group 'org.example'
    version '1.0-SNAPSHOT'

    sourceCompatibility = '17'
    targetCompatibility = '17'
    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
    }

    // 하위 모듈에서 공통으로 사용하는 세팅 추가
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'mysql:mysql-connector-java'
        implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.4'
        implementation group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: '5.2.1'
        implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-joda', version: '2.13.3'
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        runtimeOnly 'com.h2database:h2'

        /* security */
        implementation 'org.springframework.boot:spring-boot-starter-security'
        testImplementation 'org.springframework.security:spring-security-test'

        /* 파일 업로드 위한 AWS 셋팅 */
        implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    }

    test {
        useJUnitPlatform()
    }
}

project(':xcape-core') {
    // core 에서는 bootjar로 패키징 할 필요가 없으므로 추가
    bootJar.enabled = false
    jar.enabled = true

    dependencies {

    }
}

// 임시 -> 개발 착수 후 삭제
project(':xcape-batch') {
    // core 에서는 bootjar로 패키징 할 필요가 없으므로 추가
    bootJar.enabled = false
    jar.enabled = true

    dependencies {
    }
}

project(':xcape-admin') {
    dependencies {
        // 컴파일시 core 모듈을 가져온다.
        implementation project(':xcape-core')
        implementation 'org.webjars:bootstrap:5.2.3'
        implementation 'org.webjars.npm:axios:1.2.2'
    }
}

project(':xcape-api') {
    dependencies {
        implementation project(':xcape-core')
    }
}

project(':xcape-batch') {
    dependencies {
        implementation project(':xcape-core')

        implementation 'org.springframework.boot:spring-boot-starter-batch'
    }
}
